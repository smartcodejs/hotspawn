config {
  type: "view",
  schema: dataform.projectConfig.vars.schema,
  tags: ["ga4","raw","users"]
}

SELECT
  TIMESTAMP_MICROS(event_timestamp) AS event_ts,
  DATE(TIMESTAMP_MICROS(event_timestamp)) AS event_date,
  user_pseudo_id,
  user_id AS event_user_id,
  (SELECT up.value.string_value FROM UNNEST(user_properties) up WHERE up.key = 'user_id') AS userprop_user_id,
  event_name,
  (SELECT ep.value.int_value FROM UNNEST(event_params) ep WHERE ep.key = 'ga_session_id') AS ga_session_id,
  geo.country     AS geo_country,
  device.category AS device_category,
  platform
FROM `hotspawn-440312.analytics_457395054.events_*`
WHERE _TABLE_SUFFIX BETWEEN
  FORMAT_DATE('%Y%m%d', DATE_SUB(CURRENT_DATE('${dataform.projectConfig.vars.tz}'), INTERVAL ${dataform.projectConfig.vars.lookback_days}-1 DAY))
  AND FORMAT_DATE('%Y%m%d', CURRENT_DATE('${dataform.projectConfig.vars.tz}'))
