config {
  type: "view",
  schema: dataform.projectConfig.vars.schema,
  tags: ["ga4","raw","users"]
}

-- For user-level rollups & first-touch attribution
SELECT
  TIMESTAMP_MICROS(event_timestamp) AS event_ts,
  DATE(TIMESTAMP_MICROS(event_timestamp)) AS event_date,
  user_pseudo_id,
  user_id AS event_user_id,
  (SELECT up.value.string_value FROM UNNEST(user_properties) up WHERE up.key = 'user_id') AS userprop_user_id,

  traffic_source.source AS ts_source,
  traffic_source.medium AS ts_medium,
  traffic_source.name   AS ts_campaign,

  (SELECT ep.value.int_value FROM UNNEST(event_params) ep WHERE ep.key = 'ga_session_id') AS ga_session_id,
  event_name,
  geo.country     AS geo_country,
  device.category AS device_category,
  platform
FROM `${dataform.projectConfig.defaultDatabase}.${dataform.projectConfig.vars.ga4_dataset}.events_*`
WHERE _TABLE_SUFFIX BETWEEN '${dataform.projectConfig.vars.raw_start_date}'
                        AND FORMAT_DATE('%Y%m%d', CURRENT_DATE())
