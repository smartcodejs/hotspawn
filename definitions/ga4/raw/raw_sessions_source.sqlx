config {
  type: "view",
  schema: dataform.projectConfig.vars.schema,
  tags: ["ga4","raw","sessions"]
}

/* Light projection of all events with session/UTM + geo/device info */
SELECT
  _TABLE_SUFFIX                                                AS tbl_suffix,
  TIMESTAMP_MICROS(event_timestamp)                            AS event_ts,
  DATE(TIMESTAMP_MICROS(event_timestamp))                      AS event_date,
  user_pseudo_id,
  (SELECT ep.value.int_value FROM UNNEST(event_params) ep
    WHERE ep.key = 'ga_session_id')                            AS ga_session_id,
  event_name,

  -- explicit UTM on the event (typical on session_start)
  (SELECT ep.value.string_value FROM UNNEST(event_params) ep WHERE ep.key = 'source')   AS session_source,
  (SELECT ep.value.string_value FROM UNNEST(event_params) ep WHERE ep.key = 'medium')   AS session_medium,
  (SELECT ep.value.string_value FROM UNNEST(event_params) ep WHERE ep.key = 'campaign') AS session_campaign,

  -- optional site flag
  (SELECT ep.value.string_value FROM UNNEST(event_params) ep WHERE ep.key = 'site_name') AS site_name,

  -- geo/device/platform snapshot
  geo.country     AS geo_country,
  device.category AS device_category,
  platform
FROM `hotspawn-440312.analytics_457395854.events_*`
WHERE _TABLE_SUFFIX BETWEEN '20240101'
  AND FORMAT_DATE('%Y%m%d', CURRENT_DATE())
