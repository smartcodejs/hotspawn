config {
  type: "table",
  schema: dataform.projectConfig.vars.schema,
  tags: ["ga4","intermediate","user"],
  bigquery: {
    partitionBy: "DATE(first_seen_ts)",
    clusterBy: ["user_pseudo_id"],
    requirePartitionFilter: true
  }
}

-- Base events taken from the raw view
WITH base AS (
  SELECT
    user_pseudo_id,
    event_name,
    event_ts,
    ts_source, ts_medium, ts_campaign,
    geo_country, device_category, device_operating_system,
    mobile_brand_name, mobile_model_name, platform
  FROM ${ref("raw_events")}
),

-- First/last markers and first-known attributes
marks AS (
  SELECT
    user_pseudo_id,
    MIN(event_ts) AS first_seen_ts,
    MIN(IF(event_name = 'session_start', event_ts, NULL)) AS first_session_ts,
    MAX(IF(event_name = 'session_start', event_ts, NULL)) AS last_session_ts,
    COUNTIF(event_name = 'session_start') AS total_sessions,
    MIN(IF(event_name IN ('sign_up', 'user_signup'), event_ts, NULL)) AS signup_ts,

    ARRAY_AGG(ts_source            IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_source,
    ARRAY_AGG(ts_medium            IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_medium,
    ARRAY_AGG(ts_campaign          IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_campaign,
    ARRAY_AGG(geo_country          IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_country,
    ARRAY_AGG(device_category      IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_device_category,
    ARRAY_AGG(device_operating_system IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_os,
    ARRAY_AGG(mobile_brand_name    IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_mobile_brand,
    ARRAY_AGG(mobile_model_name    IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_mobile_model,
    ARRAY_AGG(platform             IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_platform
  FROM base
  GROUP BY user_pseudo_id
)

SELECT
  user_pseudo_id,
  first_seen_ts,
  DATE(first_seen_ts, "${dataform.projectConfig.vars.timezone}") AS first_seen_date,

  signup_ts,
  DATE(signup_ts, "${dataform.projectConfig.vars.timezone}") AS signup_date,
  IF(signup_ts IS NOT NULL, 1, 0) AS is_signed_up,

  first_session_ts,
  last_session_ts,
  total_sessions,

  first_source, first_medium, first_campaign,
  first_country, first_device_category, first_os,
  first_mobile_brand, first_mobile_model, first_platform
FROM marks
