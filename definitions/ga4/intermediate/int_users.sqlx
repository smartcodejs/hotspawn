config {
  type: "incremental",
  schema: dataform.projectConfig.vars.schema,
  name: "int_users",
  tags: ["ga4","intermediate","users"],
  bigquery: { partitionBy: "first_seen", clusterBy: ["user_pseudo_id"], requirePartitionFilter: true },
  uniqueKey: ["user_pseudo_id"]
}

WITH recent_users AS (
  SELECT user_pseudo_id
  FROM ${ref("raw_events")}
  ${ incremental()
      ? " WHERE event_date >= DATE_SUB(CURRENT_DATE('" +
          (dataform.projectConfig.vars.timezone || "Europe/Bratislava") +
          "'), INTERVAL " + (dataform.projectConfig.vars.backfill_days || 7) + " DAY)"
      : "" }
  GROUP BY 1
),
ub AS (
  SELECT e.*
  FROM ${ref("raw_events")} e
  ${ incremental() ? " JOIN recent_users USING (user_pseudo_id)" : "" }
),
agg AS (
  SELECT
    user_pseudo_id,
    MIN(event_date) AS first_seen,
    COUNT(DISTINCT session_key) AS sessions_by_key,
    COUNT(*) AS event_rows,
    ARRAY_AGG(IF(event_name IN ('sign_up','user_signup'), event_ts, NULL) IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS signup_timestamp,
    COALESCE(
      ARRAY_AGG(event_user_id    IGNORE NULLS ORDER BY event_ts DESC)[SAFE_OFFSET(0)],
      ARRAY_AGG(userprop_user_id IGNORE NULLS ORDER BY event_ts DESC)[SAFE_OFFSET(0)]
    ) AS user_id,
    ARRAY_AGG(ts_source   IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_utm_source,
    ARRAY_AGG(ts_medium   IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_utm_medium,
    ARRAY_AGG(ts_campaign IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_utm_campaign,
    ARRAY_AGG(geo_country             IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_geo_country,
    ARRAY_AGG(device_category         IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_device_category,
    ARRAY_AGG(device_operating_system IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_device_operating_system,
    ARRAY_AGG(mobile_brand_name       IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_mobile_brand_name,
    ARRAY_AGG(mobile_model_name       IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_mobile_model_name,
    ARRAY_AGG(platform                IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_platform
  FROM ub
  GROUP BY 1
)

SELECT
  user_pseudo_id,
  user_id,
  first_seen,
  CASE WHEN sessions_by_key > 0 THEN sessions_by_key
       WHEN event_rows      > 0 THEN 1 ELSE 0 END AS total_sessions,
  signup_timestamp,
  DATE(signup_timestamp, '${dataform.projectConfig.vars.timezone || "Europe/Bratislava"}') AS signup_date,
  signup_timestamp IS NOT NULL AS signup_occurred,
  first_utm_source,
  first_utm_medium,
  first_utm_campaign,
  first_geo_country,
  first_device_category,
  first_device_operating_system,
  first_mobile_brand_name,
  first_mobile_model_name,
  first_platform
