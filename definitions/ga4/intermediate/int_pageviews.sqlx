config {
  type: "table",
  schema: dataform.projectConfig.vars.schema,
  tags: ["ga4","intermediate","pageviews"],
  bigquery: { partitionBy: "session_date", clusterBy: ["user_pseudo_id","session_id"] }
}

WITH pv AS (
  SELECT
    session_date,
    user_pseudo_id,
    CONCAT(user_pseudo_id, '-', CAST(ga_session_id AS STRING)) AS session_id,
    event_ts AS pageview_time,
    page_url,
    page_title
  FROM ${ref("raw_page_source")}
),
url_parts AS (
  SELECT
    pv.*,
    COALESCE(NULLIF(SPLIT(REGEXP_EXTRACT(page_url, r'^https?://[^/]+/([^?#]*)'), '/')[SAFE_OFFSET(0)], ''), 'hotspawn') AS theme_category,
    COALESCE(NULLIF(SPLIT(REGEXP_EXTRACT(page_url, r'^https?://[^/]+/([^?#]*)'), '/')[SAFE_OFFSET(1)], ''), 'hotspawn') AS page_category
  FROM pv
),
windowed AS (
  SELECT
    p.*,
    ROW_NUMBER() OVER (PARTITION BY p.session_id ORDER BY p.pageview_time) AS pageview_number,
    LEAD(p.page_url) OVER (PARTITION BY p.session_id ORDER BY p.pageview_time) AS next_page_url,
    LAST_VALUE(p.page_url) OVER (PARTITION BY p.session_id ORDER BY p.pageview_time
      ROWS BETWEEN UNBOUNDED PRECEDING AND UNBOUNDED FOLLOWING) AS exit_page_url
  FROM url_parts p
)

SELECT
  session_date,
  user_pseudo_id,
  session_id,
  page_url,
  page_title,
  theme_category,
  page_category,
  NULL AS author_name,
  NULL AS max_scroll_percentage,
  NULL AS max_time_on_page,
  pageview_number,
  next_page_url,
  NULL AS user_type,
  IF(pageview_number = 1, 1, 0) AS isLandingPage,
  IF(pageview_number = MAX(pageview_number) OVER (PARTITION BY session_id), 1, 0) AS isExitPage,
  exit_page_url
FROM windowed
