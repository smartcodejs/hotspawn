config {
  type: "table",
  bigquery: { partitionBy: "first_seen", clusterBy: ["user_pseudo_id"] },
  tags: ["ga4","users","final"]
}

WITH user_aggregates AS (
  SELECT
    user_pseudo_id,

    -- Core rollups
    MIN(event_date) AS first_seen,
    COUNT(DISTINCT session_key) AS sessions_by_key,
    COUNT(DISTINCT IF(site_name = 'Microsite', session_key, NULL)) AS microsite_sessions,
    COUNT(1) AS event_rows,

    -- First signup time
    ARRAY_AGG(
      IF(event_name IN ('sign_up','user_signup'), event_ts, NULL)
      IGNORE NULLS ORDER BY event_ts ASC
    )[SAFE_OFFSET(0)] AS signup_timestamp,

    -- Latest stitched user id
    COALESCE(
      ARRAY_AGG(event_user_id    IGNORE NULLS ORDER BY event_ts DESC)[SAFE_OFFSET(0)],
      ARRAY_AGG(userprop_user_id IGNORE NULLS ORDER BY event_ts DESC)[SAFE_OFFSET(0)]
    ) AS user_id,

    -- First-touch attribution
    ARRAY_AGG(ts_source   IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_ts_source,
    ARRAY_AGG(ts_medium   IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_ts_medium,
    ARRAY_AGG(ts_campaign IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_ts_campaign,

    -- First device / geo
    ARRAY_AGG(geo_country             IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_geo_country,
    ARRAY_AGG(device_category         IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_device_category,
    ARRAY_AGG(device_operating_system IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_device_operating_system,
    ARRAY_AGG(mobile_brand_name       IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_mobile_brand_name,
    ARRAY_AGG(mobile_model_name       IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_mobile_model_name,
    ARRAY_AGG(platform                IGNORE NULLS ORDER BY event_ts ASC)[SAFE_OFFSET(0)] AS first_platform
  FROM ${ref("stg_ga4_events")}
  GROUP BY user_pseudo_id
)

SELECT
  user_pseudo_id,
  user_id,
  first_seen,

  CASE
    WHEN sessions_by_key > 0 THEN sessions_by_key
    WHEN event_rows      > 0 THEN 1
    ELSE 0
  END AS total_sessions,

  signup_timestamp,
  DATE(signup_timestamp, 'Europe/Bratislava') AS signup_date,
  IF(signup_timestamp IS NULL, FALSE, TRUE)   AS signup_occurred,

  -- Compatibility names
  first_ts_source   AS first_utm_source,
  first_ts_medium   AS first_utm_medium,
  first_ts_campaign AS first_utm_campaign,

  microsite_sessions,

  first_geo_country,
  first_device_category,
  first_device_operating_system,
  first_mobile_brand_name,
  first_mobile_model_name,
  first_platform
FROM user_aggregates
